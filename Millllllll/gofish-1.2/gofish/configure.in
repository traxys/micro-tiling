dnl Process this file with autoconf to produce a configure script.
AC_INIT(gofish.h)
AM_INIT_AUTOMAKE(gofish, 1.0)
AM_CONFIG_HEADER(config.h:config.h.in)

AC_CANONICAL_HOST

dnl Extra configure arguments
gopherroot=$localstatedir/gopher
AC_ARG_WITH(gopherroot,
	[  --with-gopherroot=ROOT   Use ROOT for Gopher documents],
	[gopherroot=$withval])
AC_SUBST(gopherroot)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

dnl Checks for Sun libraries.
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(nsl, gethostbyname)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h sys/time.h syslog.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_TM

dnl Checks for library functions.

dnl We cannot use this special function since it fails under FreeBSD
dnl AC_FUNC_MMAP
AC_CHECK_FUNCS(mmap)
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(gethostname select socket strdup strerror strstr strtol uname)
AC_CHECK_FUNCS(writev daemon)

dnl Interix does not have initgroups
AC_CHECK_FUNCS(initgroups)

dnl poll does not work on the arm
dnl if this goes, AC_CANONICAL_HOST and the files
dnl config.sub and config.guess can go
case $host in
  armv4l-*) ;;
  *) AC_CHECK_FUNCS(poll) ;;
esac

dnl Add -Wall option for gcc
if test "$GCC" = "yes"; then
  CFLAGS="$CFLAGS -Wall"
fi

AC_MSG_CHECKING([mmap_cache])
AC_ARG_ENABLE(mmap-cache,
  [  --enable-mmap-cache    Enable mmap caching. EXPERIMENTAL],
  [])
if test "$enable_mmap_cache" = "yes"; then
  AC_DEFINE(MMAP_CACHE, 1, [Define this to enable mmap cache])
else
  enable_mmap_cache=no
fi
AC_MSG_RESULT([$enable_mmap_cache])

dnl Check for gopher user override.
AC_MSG_CHECKING([gopheruser])
gopheruser="gopher"
AC_ARG_WITH(gopheruser,
	[  --with-gopheruser=USER  Use USER as non-privileged user],
	[gopheruser=$withval])
AC_DEFINE_UNQUOTED(GOPHER_USER, "$gopheruser", [The non-privileged user])
AC_MSG_RESULT([$gopheruser])

AC_OUTPUT(Makefile)
